
////////////////////////////////////////////////////////////////////////////////
// Обработчики событий формы
//

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПериодВыполнения = 3;                               // Секунды.
	ОсталосьДоНачалаВыполнения = ПериодВыполнения + 1;  // Секунды.
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ВыполнитьРегламентныеЗадания();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий команд и элементов формы
//

&НаКлиенте
Процедура ПрекратитьВыполнениеИЗавершитьСеанс(Команда)
	
	ПрекратитьВыполнениеИЗавершитьСеансВыполнить();
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаВыполненияРегламентныхЗаданий(Команда)
	
	ПараметрыФормы = Новый Структура("СкрытьКомандуЗапускаОтдельногоСеанса", Истина);
	
	ОткрытьФормуМодально("Обработка.РегламентныеИФоновыеЗадания.Форма.НастройкаВыполненияРегламентныхЗаданий", ПараметрыФормы);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Вспомогательные процедуры и функции формы
//

&НаКлиенте
Процедура ПрекратитьВыполнениеИЗавершитьСеансВыполнить()
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьРегламентныеЗадания()
	
	Перезапустить = Ложь;
	Если НужноПрекратитьВыполнение(ПараметрЗапуска, Перезапустить) Тогда
		Закрыть(?(Перезапустить, "Перезапустить", Неопределено));
	Иначе
		ОсталосьДоНачалаВыполнения = ОсталосьДоНачалаВыполнения - 1;
		Если ОсталосьДоНачалаВыполнения <= 0 Тогда
			
			ОсталосьДоНачалаВыполнения = ПериодВыполнения;
			
			СтрокаСостояния = НСтр("ru = 'Выполняются задания, дождитесь завершения... '");
			ОбновитьОтображениеДанных();
			
			РегламентныеЗаданияСервер.ВыполнитьРегламентныеЗадания();
		КонецЕсли;
	КонецЕсли;
	
	// Прекращение выполнения, если пользователь нажал CTRL+BREAK
	ПодключитьОбработчикОжидания("ПрекратитьВыполнениеИЗавершитьСеансВыполнить", 1, Истина);
	ОбработкаПрерыванияПользователя();
	ОтключитьОбработчикОжидания("ПрекратитьВыполнениеИЗавершитьСеансВыполнить");
	
	ПодключитьОбработчикОжидания("ВыполнитьРегламентныеЗадания", 1, Истина);
	СтрокаСостояния = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'До начала выполнения осталось секунд: %1'"),
		Строка(ОсталосьДоНачалаВыполнения) );
	
КонецПроцедуры

&НаСервере
Функция НужноПрекратитьВыполнение(ПараметрЗапуска, Перезапустить)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Перезапустить = КонфигурацияБазыДанныхИзмененаДинамически();
	
	Возврат Перезапустить ИЛИ
	        РегламентныеЗаданияСервер.РодительскийСеансЗаданИЗавершен(ПараметрЗапуска) ИЛИ
	        НЕ РегламентныеЗаданияСервер.ТекущийСеансВыполняетРегламентныеЗадания();
	
КонецФункции

