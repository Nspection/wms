
#Область ОбработчикиСобытийФормы

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНастройкиИспользования

&НаКлиенте
Процедура НастройкиИспользованияВключеноПриИзменении(Элемент)
	ТекущаяСтрока = Элементы.НастройкиИспользования.ТекущиеДанные;
	Подстроки = Элементы.НастройкиИспользования.ТекущиеДанные.ПолучитьЭлементы();
	Если Подстроки.Количество() > 0 Тогда
		Для каждого Подстрока Из Подстроки Цикл
			Если Подстрока.Включено <> ТекущаяСтрока.Включено Тогда
				ИДСтроки = Подстрока.ПолучитьИдентификатор();
				ИзменитьНастройкуПоСтроке(Подстрока.ОбъектМетаданные, ТекущаяСтрока.Включено, ИДСтроки);
				// СтрокаДерева = НастройкиИспользования.НайтиПоИдентификатору(Подстрока.ПолучитьИдентификатор());
				// СтрокаДерева.Включено = Включено;
			КонецЕсли; 
		КонецЦикла; 
	Иначе
		ИДСтроки = ТекущаяСтрока.ПолучитьИдентификатор();
		ИзменитьНастройкуПоСтроке(ТекущаяСтрока.ОбъектМетаданные, ТекущаяСтрока.Включено, ИДСтроки);				
	КонецЕсли; 
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьДерево(Команда)
	ЗаполнитьДеревоНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ИсторияИзмененийДанных(Команда)
	ПодключитьВнешнююОбработкуНаСервере();
	ОткрытьФорму("ВнешняяОбработка.StandardDataChangeHistory.Форма");	
КонецПроцедуры 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьДеревоНаСервере()

	ДЗНастройки = ДанныеФормыВЗначение(НастройкиИспользования, Тип("ДеревоЗначений"));
	
	ТипыОбъектов = ДЗНастройки.Строки;
	ТипыОбъектов.Очистить();	
	
	ДополнитьДеревоСтрокамиПоТипуМетаданных(ТипыОбъектов, "Константы", "Константы");
	ДополнитьДеревоСтрокамиПоТипуМетаданных(ТипыОбъектов, "Справочники", "Справочники");
	ДополнитьДеревоСтрокамиПоТипуМетаданных(ТипыОбъектов, "Документы", "Документы");
	ДополнитьДеревоСтрокамиПоТипуМетаданных(ТипыОбъектов, "РегистрыСведений", "Регистры сведений");
	ДополнитьДеревоСтрокамиПоТипуМетаданных(ТипыОбъектов, "Задачи", "Задачи");
	ДополнитьДеревоСтрокамиПоТипуМетаданных(ТипыОбъектов, "БизнесПроцессы", "Бизнес процессы");
	ДополнитьДеревоСтрокамиПоТипуМетаданных(ТипыОбъектов, "ПланыСчетов", "Планы счетов");
	ДополнитьДеревоСтрокамиПоТипуМетаданных(ТипыОбъектов, "ПланыВидовХарактеристик", "Планы видов характеристик");
	ДополнитьДеревоСтрокамиПоТипуМетаданных(ТипыОбъектов, "ПланыВидовРасчета", "Планы видов расчета");
	                                                              
	ЗначениеВДанныеФормы(ДЗНастройки, НастройкиИспользования);
	                                                        
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ДополнитьДеревоСтрокамиПоТипуМетаданных(ТипыОбъектов, ТипМетаданных, Синоним);

	СтрокаТипОбъекта = ТипыОбъектов.Добавить();
	СтрокаТипОбъекта.ОбъектМетаданные = Синоним;
	СтрокаТипОбъекта.Включено = Ложь;
	
	Для каждого МетаданныеОбъект Из Метаданные[ТипМетаданных] Цикл
		Попытка
			НастройкиИД = ИсторияДанных.ПолучитьНастройки(МетаданныеОбъект);
			
			СписокОбъектов = СтрокаТипОбъекта.Строки;
			НоваяСтрока = СписокОбъектов.Добавить();
			НоваяСтрока.ОбъектМетаданные = ТипМетаданных + "." + МетаданныеОбъект.Имя;
			
			Если НастройкиИД <> Неопределено И НастройкиИД.Использование Тогда
				НоваяСтрока.Включено = Истина;
			Иначе
				НоваяСтрока.Включено = Ложь;
			КонецЕсли; 
		Исключение
			// не для всех объектов можно использовать историю данных
			Сообщить(ОписаниеОшибки());
		КонецПопытки;		
	КонецЦикла; 
	
	Если СтрокаТипОбъекта.Строки.Количество() = 0 Тогда
		ТипыОбъектов.Удалить(СтрокаТипОбъекта);			
	КонецЕсли; 
	
КонецПроцедуры 

&НаСервере
Процедура НастройкиИспользованияВключеноПриИзмененииНаСервере(ДанныеИмяОбъекта, Включено)
	
	ОбъектМетаданных = Метаданные[ДанныеИмяОбъекта[0]][ДанныеИмяОбъекта[1]];
	НастройкиИсторииДанных = Новый НастройкиИсторииДанных;
	НастройкиИсторииДанных.Использование = Включено;
	ИсторияДанных.УстановитьНастройки(ОбъектМетаданных, НастройкиИсторииДанных); 			
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьНастройкуПоСтроке(ОбъектМетаданные, Включено, ИДСтроки)
	
	Попытка
		ТекСтрока = НастройкиИспользования.НайтиПоИдентификатору(ИДСтроки);
		ДанныеИмяОбъекта = СтрРазделить(ОбъектМетаданные, ".");
		Если ДанныеИмяОбъекта.Количество() = 2 Тогда
			НастройкиИспользованияВключеноПриИзмененииНаСервере(ДанныеИмяОбъекта, Включено);
			ТекСтрока.Включено = Включено;
		Иначе
			ТекСтрока.Включено = Не	Включено;
		КонецЕсли; 
	Исключение
	    Сообщить(ОбъектМетаданные + ". Нарушение прав доступа.");
	КонецПопытки; 
	
КонецПроцедуры

&НаСервере
Процедура ПодключитьВнешнююОбработкуНаСервере()
    ВнешниеОбработки.Подключить("v8res://mngbase/StandardDataChangeHistory.epf", "StandardDataChangeHistory", false);
КонецПроцедуры
#КонецОбласти
