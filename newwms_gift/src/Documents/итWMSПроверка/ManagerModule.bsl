Процедура СформироватьПечатнуюФорму(Ссылка,ТабличныйДокумент,СтруктураКраткогоСодежимого=Неопределено) Экспорт 
	Если СтруктураКраткогоСодежимого=Неопределено Тогда 
		СтруктураКраткогоСодежимого=Новый Структура;
	КонецЕсли;	
    Макет=ПолучитьМакет("ОбщаяИнформацияПроверки");
	СформироватьПечатнуюЗаполнениеШапки(Макет, Ссылка, ТабличныйДокумент,СтруктураКраткогоСодежимого);
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(итWMSПроверкаТовары.Номенклатура) КАК Номенклатура,
		|	итWMSПроверкаТовары.ДатаРозлива,
		|	СУММА(итWMSПроверкаТовары.Количество) КАК Количество,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(итWMSПроверкаТовары.Номенклатура.ЕдиницаХраненияОстатков) КАК Ед,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(итWMSПроверкаТовары.Номенклатура.ЕдиницаИзмеренияМест) КАК ЕдМест,
		|	итWMSПроверкаТовары.Номенклатура.ЕдиницаИзмеренияМест.Коэффициент КАК ЕдиницаИзмеренияМестКоэффициент,
		|	СУММА(итWMSПроверкаТовары.Количество / ВЫБОР
		|			КОГДА ЕСТЬNULL(итWMSПроверкаТовары.Номенклатура.ЕдиницаИзмеренияМест.Коэффициент, 1) = 0
		|				ТОГДА 1
		|			ИНАЧЕ ЕСТЬNULL(итWMSПроверкаТовары.Номенклатура.ЕдиницаИзмеренияМест.Коэффициент, 1)
		|		КОНЕЦ) КАК КоличествоМест
		|ИЗ
		|	Документ.итWMSПроверка.Товары КАК итWMSПроверкаТовары
		|ГДЕ
		|	итWMSПроверкаТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	итWMSПроверкаТовары.ДатаРозлива,
		|	итWMSПроверкаТовары.Номенклатура.ЕдиницаИзмеренияМест.Коэффициент,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(итWMSПроверкаТовары.Номенклатура),
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(итWMSПроверкаТовары.Номенклатура.ЕдиницаХраненияОстатков),
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(итWMSПроверкаТовары.Номенклатура.ЕдиницаИзмеренияМест)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(итWMSПроверкаТовары.Количество / ВЫБОР
		|			КОГДА ЕСТЬNULL(итWMSПроверкаТовары.Номенклатура.ЕдиницаИзмеренияМест.Коэффициент, 1) = 0
		|				ТОГДА 1
		|			ИНАЧЕ ЕСТЬNULL(итWMSПроверкаТовары.Номенклатура.ЕдиницаИзмеренияМест.Коэффициент, 1)
		|		КОНЕЦ) КАК КоличествоМест,
		|	СУММА(итWMSПроверкаТовары.Количество) КАК Количество,
		|	ВложенныйЗапрос.ИдентификаторУпаковки КАК КоличествоПаллет
		|ИЗ
		|	Документ.итWMSПроверка.Товары КАК итWMSПроверкаТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			КОЛИЧЕСТВО(РАЗЛИЧНЫЕ итWMSПроверкаТовары.ИдентификаторУпаковки) КАК ИдентификаторУпаковки
		|		ИЗ
		|			Документ.итWMSПроверка.Товары КАК итWMSПроверкаТовары
		|		ГДЕ
		|			итWMSПроверкаТовары.Ссылка = &Ссылка) КАК ВложенныйЗапрос
		|		ПО (ИСТИНА)
		|ГДЕ
		|	итWMSПроверкаТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.ИдентификаторУпаковки";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
    ТаблицаДанных=новый ТаблицаЗначений;
	ОпределитьКолонкиТаблицы(ТаблицаДанных);
	ВыборкаДетальнойЗаписи=МассивРезультатов[0].Выбрать();
	Пока ВыборкаДетальнойЗаписи.Следующий() Цикл 
		ОбластьСтрокиТовары=Макет.ПолучитьОбласть("СтрокиТовары");
		ОбластьСтрокиТовары.Параметры.Заполнить(ВыборкаДетальнойЗаписи);
		ТабличныйДокумент.Вывести(ОбластьСтрокиТовары);
		ЗаполнитьЗначенияСвойств(ТаблицаДанных.Добавить(),ВыборкаДетальнойЗаписи);
	КонецЦикла;
	СтруктураКраткогоСодежимого.Вставить("ТаблицаДанных",ТаблицаДанных);
	ВыборкаИтогов=МассивРезультатов[1].Выбрать();
	ОбластьИтогов=Макет.ПолучитьОбласть("Итого");
	Пока ВыборкаИтогов.Следующий() Цикл
		ОбластьИтогов.Параметры.КоличествоПаллет= ВыборкаИтогов.КоличествоПаллет;
		ОбластьИтогов.Параметры.КоличествоИтого=ВыборкаИтогов.Количество;
		ОбластьИтогов.Параметры.КоличествоМестИтого=ВыборкаИтогов.КоличествоМест;
		СтруктураКраткогоСодежимого.Вставить("КоличествоПаллет",ВыборкаИтогов.КоличествоПаллет);
		СтруктураКраткогоСодежимого.Вставить("КоличествоИтого",ВыборкаИтогов.Количество);
		СтруктураКраткогоСодежимого.Вставить("КоличествоМестИтого",ВыборкаИтогов.КоличествоМест);
	КонецЦикла;	
	ТабличныйДокумент.Вывести(ОбластьИтогов);
	ОбластьПодписи=Макет.ПолучитьОбласть("Подписи");
	ТабличныйДокумент.Вывести(ОбластьПодписи);
КонецПроцедуры

Процедура ОпределитьКолонкиТаблицы( ТаблицаДанных)
	
	ТаблицаДанных.Колонки.Добавить("Номенклатура");
	ТаблицаДанных.Колонки.Добавить("ДатаРозлива");
	ТаблицаДанных.Колонки.Добавить("Количество");
	ТаблицаДанных.Колонки.Добавить("Ед");
	ТаблицаДанных.Колонки.Добавить("ЕдМест");
	ТаблицаДанных.Колонки.Добавить("КоличествоМест");

КонецПроцедуры

Процедура СформироватьПечатнуюЗаполнениеШапки( Макет,  Ссылка,  ТабличныйДокумент,СтруктураКраткогоСодежимого)
	
	
	ОбластьОрганизация=Макет.ПолучитьОбласть("Организация");
	ОбластьКонтрагент=Макет.ПолучитьОбласть("Контрагент");
	ОбластьОрганизация.Параметры.Организация=Строка(Ссылка.Организация);
	ОбластьКонтрагент.Параметры.Контрагент=Строка(Ссылка.Контрагент);
	ТабличныйДокумент.Вывести(ОбластьОрганизация);
	ТабличныйДокумент.Вывести(ОбластьКонтрагент);
	ДанныеВремяДатыОтгрузки=ПолучитьДатуВремяОтгрузки(Ссылка);
	ОбластьДатаВремяОтгрузки=Макет.ПолучитьОбласть("ДатаВремяОтгрузки");
	ОбластьДатаВремяОтгрузки.Параметры.Датаотгрузки=Формат(ДанныеВремяДатыОтгрузки.ДатаОтгрузки,"ДФ=dd.MM.yyyy");
	ОбластьДатаВремяОтгрузки.Параметры.Времяотгрузки=Формат(ДанныеВремяДатыОтгрузки.ВремяПогрузки,"ДЛФ=T");
	ОбластьШапкиТовары=Макет.ПолучитьОбласть("ШапкаТовары");
	ТабличныйДокумент.Вывести(ОбластьДатаВремяОтгрузки);
	ТабличныйДокумент.Вывести(ОбластьШапкиТовары);
	СтруктураКраткогоСодежимого.Вставить("Организация",Строка(Ссылка.Организация));
    СтруктураКраткогоСодежимого.Вставить("Контрагент",Строка(Ссылка.Контрагент));
    СтруктураКраткогоСодежимого.Вставить("Датаотгрузки",ДанныеВремяДатыОтгрузки.ДатаОтгрузки);
    СтруктураКраткогоСодежимого.Вставить("ВремяПогрузки",ДанныеВремяДатыОтгрузки.ВремяПогрузки);
КонецПроцедуры


Функция ПолучитьДатуВремяОтгрузки(Ссылка)
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	итWMSПроверкаитОснования.Документ
		|ПОМЕСТИТЬ ВТНаборки
		|ИЗ
		|	Документ.итWMSПроверка.итОснования КАК итWMSПроверкаитОснования
		|ГДЕ
		|	итWMSПроверкаитОснования.Ссылка = &Ссылка
		|	И ТИПЗНАЧЕНИЯ(итWMSПроверкаитОснования.Документ) = ТИП(Документ.итwmsНаборка)
		|
		|СГРУППИРОВАТЬ ПО
		|	итWMSПроверкаитОснования.Документ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	итWMSНаборка.итОснование КАК Заказ
		|ПОМЕСТИТЬ ВтЗаказПокупателя
		|ИЗ
		|	ВТНаборки КАК ВТНаборки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.итWMSНаборка КАК итWMSНаборка
		|		ПО ВТНаборки.Документ = итWMSНаборка.Ссылка
		|ГДЕ
		|	ТИПЗНАЧЕНИЯ(итWMSНаборка.итОснование) = ТИП(Документ.ЗаказПокупателя)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(ЗаказПокупателя.ДатаОтгрузки) КАК ДатаОтгрузки,
		|	МАКСИМУМ(ЗаказПокупателя.итВремяНачалаПогрузкиКакВремя) КАК итВремяНачалаПогрузкиКакВремя
		|ПОМЕСТИТЬ ДатаВремяНачалаОтгрузки
		|ИЗ
		|	ВтЗаказПокупателя КАК ВтЗаказПокупателя
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказПокупателя КАК ЗаказПокупателя
		|		ПО ВтЗаказПокупателя.Заказ = ЗаказПокупателя.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(ДатаВремяНачалаОтгрузки.ДатаОтгрузки, ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)) КАК ДатаОтгрузки,
		|	ЕСТЬNULL(ДатаВремяНачалаОтгрузки.итВремяНачалаПогрузкиКакВремя, ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)) КАК ВремяПогрузки
		|ИЗ
		|	ДатаВремяНачалаОтгрузки КАК ДатаВремяНачалаОтгрузки";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
    СтруктураОтвета=новый Структура;
	СтруктураОтвета.Вставить("ДатаОтгрузки",'00010101');
	СтруктураОтвета.Вставить("ВремяПогрузки",'00010101');
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		СтруктураОтвета.ДатаОтгрузки=ВыборкаДетальныеЗаписи.ДатаОтгрузки;
		СтруктураОтвета.ВремяПогрузки=ВыборкаДетальныеЗаписи.ВремяПогрузки;
	КонецЦикла;
	
	Возврат СтруктураОтвета;	
	КонецФункции
	
	
Процедура ВнестиИзменениеДанныхТСДВДокумент(Документ) Экспорт 
	итWMSОбработчикДанныхПроверки.ВнестиИзмененияВДокументПроверки(Документ);
КонецПроцедуры