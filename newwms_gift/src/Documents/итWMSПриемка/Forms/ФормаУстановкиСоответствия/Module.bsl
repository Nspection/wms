
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если не Параметры.Свойство("ДанныеСкладскогоУчета") Тогда 
		Отказ=Истина;
		Сообщить("Отсутствует ключевое свойство ДанныеСкладскогоУчета");
		Возврат
	КонецЕсли;	
	Если не Параметры.Свойство("ДокументыПомарочногоУчета") Тогда 
		Отказ=Истина;
		Сообщить("Отсутствует ключевое свойство ДокументыПомарочногоУчета");
		Возврат
	КонецЕсли;	
	ЗаполнитьДанныеСкладскогоУчета(Параметры.ДанныеСкладскогоУчета);
	ЗаполнитьДанныеДокументов(Параметры.ДокументыПомарочногоУчета);
	ЗаполнитьСоотвИзДокумента(Параметры.ТаблицаСоотв);
	УстановитьСпискиВыбораПолей();
КонецПроцедуры
&НаСервере
Процедура ЗаполнитьСоотвИзДокумента(ТаблицаСоотв)
	ТаблицаСоответсвий.Очистить();
	Для Каждого стр из ТаблицаСоотв Цикл 
		НоваяСтрока=ТаблицаСоответсвий.Добавить();
		НоваяСтрока.СправкаБ=стр.СправкаБ;
		НоваяСтрока.АлкогольнаяПродукция=стр.АлкогольнаяПродукция;
        НоваяСтрока.Номенклатура=стр.Номенклатура;
        НоваяСтрока.ДатаРозлива=стр.ДатаРозлива;

		
		КонецЦикла;
	КонецПроцедуры

&НаСервере
Процедура УстановитьСоответствиеНаСервере()
	МенеджерВременныхТаблиц=новый МенеджерВременныхТаблиц;	
	ПолучитьСписокСправокБиАлкогольнойПродукции(МенеджерВременныхТаблиц);
	ПеренестиТаблицуНоменклатурыВоВременнуюТаблицу(МенеджерВременныхТаблиц);
	ЗаписатьСоответсвия(МенеджерВременныхТаблиц);
КонецПроцедуры


&НаСервере
Процедура ПолучитьСписокСправокБиАлкогольнойПродукции(МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДанныеМарокЕГАИСМарки.Марка) КАК Количество,
	|	ДанныеМарокЕГАИСМарки.СправкаБ КАК СправкаБ,
	|	ДанныеМарокЕГАИСМарки.АлкогольнаяПродукция КАК АлкогольнаяПродукция
	|ПОМЕСТИТЬ ДанныеПМУ
	|ИЗ
	|	Документ.ДанныеМарокЕГАИС.Марки КАК ДанныеМарокЕГАИСМарки
	|ГДЕ
	|	ДанныеМарокЕГАИСМарки.Ссылка В(&Документы)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеМарокЕГАИСМарки.СправкаБ,
	|	ДанныеМарокЕГАИСМарки.АлкогольнаяПродукция";
	
	Запрос.УстановитьПараметр("Документы", ДокументыПомарочногоУчета.Выгрузить().ВыгрузитьКолонку("Документ"));
	
	//@skip-warning
	РезультатЗапроса = Запрос.Выполнить();
	
	
КонецПроцедуры
&НаСервере
Процедура ПеренестиТаблицуНоменклатурыВоВременнуюТаблицу(МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц=МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Таблица.Номенклатура,
	|	Таблица.ДатаРозлива,
	|	Таблица.Количество
	|ПОМЕСТИТЬ Вт
	|ИЗ
	|	&Таблица КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	алкСоответствияАлкогольнойПродукцииЕГАИСИНоменклатуры.АлкогольнаяПродукция,
	|	Вт.Номенклатура,
	|	Вт.ДатаРозлива,
	|	Вт.Количество
	|ПОМЕСТИТЬ ВтДанныеНоменклатуры
	|ИЗ
	|	Вт КАК Вт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.алкСоответствияАлкогольнойПродукцииЕГАИСИНоменклатуры КАК алкСоответствияАлкогольнойПродукцииЕГАИСИНоменклатуры
	|		ПО Вт.Номенклатура = алкСоответствияАлкогольнойПродукцииЕГАИСИНоменклатуры.Номенклатура
	|
	|СГРУППИРОВАТЬ ПО
	|	алкСоответствияАлкогольнойПродукцииЕГАИСИНоменклатуры.АлкогольнаяПродукция,
	|	Вт.Номенклатура,
	|	Вт.ДатаРозлива,
	|	Вт.Количество
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Вт";
	
	Запрос.УстановитьПараметр("Таблица",ДанныеСкладскогоУчета.Выгрузить());
	
	//@skip-warning
	РезультатЗапроса = Запрос.Выполнить();
	
	
	
	
КонецПроцедуры
&НаСервере		
Процедура ЗаписатьСоответсвия(МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц=МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВтДанныеНоменклатуры.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ВтДанныеНоменклатуры.Номенклатура КАК Номенклатура,
	|	ВтДанныеНоменклатуры.ДатаРозлива КАК ДатаРозлива,
	|	ВтДанныеНоменклатуры.Количество КАК Количество,
	|	МАКСИМУМ(ДанныеПМУ.СправкаБ) КАК СправкаБ
	|ИЗ
	|	ВтДанныеНоменклатуры КАК ВтДанныеНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеПМУ КАК ДанныеПМУ
	|		ПО ВтДанныеНоменклатуры.АлкогольнаяПродукция = ДанныеПМУ.АлкогольнаяПродукция
	|			И ВтДанныеНоменклатуры.Количество = ДанныеПМУ.Количество
	|
	|СГРУППИРОВАТЬ ПО
	|	ВтДанныеНоменклатуры.АлкогольнаяПродукция,
	|	ВтДанныеНоменклатуры.Номенклатура,
	|	ВтДанныеНоменклатуры.ДатаРозлива,
	|	ВтДанныеНоменклатуры.Количество";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	ТаблицаСоответсвий.Очистить();	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		НоваяСтрока=ТаблицаСоответсвий.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,ВыборкаДетальныеЗаписи);
	КонецЦикла;
	
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСоответствие(Команда)
	УстановитьСоответствиеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	МассивТаблицы=ТаблицаЗначенийВМассив("ТаблицаСоответсвий");
	ОповеститьОВыборе(МассивТаблицы);
КонецПроцедуры



&НаСервере
Процедура ЗаполнитьДанныеСкладскогоУчета(ДанныеСкладскогоУчетаДанные)
	ДанныеСкладскогоУчета.Очистить();
	Для Каждого стр из ДанныеСкладскогоУчетаДанные цикл
		НоваяСтрока=ДанныеСкладскогоУчета.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,стр);
	КонецЦикла;
КонецПроцедуры
&НаСервере
Процедура ЗаполнитьДанныеДокументов(ДокументыПомарочногоУчетаДанные)
	ДокументыПомарочногоУчета.Очистить();
	Для Каждого стр из ДокументыПомарочногоУчетаДанные цикл
		НоваяСтрока=ДокументыПомарочногоУчета.Добавить();
		НоваяСтрока.Документ=стр;
	КонецЦикла;
КонецПроцедуры
&НаСервере
Процедура УстановитьСпискиВыбораПолей()
	МенеджерВременныхТаблиц=новый МенеджерВременныхТаблиц;	
	ПолучитьСписокСправокБиАлкогольнойПродукции(МенеджерВременныхТаблиц);
	ПеренестиТаблицуНоменклатурыВоВременнуюТаблицу(МенеджерВременныхТаблиц);
	УстановитьСпискиВыбораРезультат(МенеджерВременныхТаблиц);
КонецПроцедуры


&НаСервере		
Процедура УстановитьСпискиВыбораРезультат(МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц=МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВтДанныеНоменклатуры.Номенклатура,
	|	ВтДанныеНоменклатуры.ДатаРозлива
	|ИЗ
	|	ВтДанныеНоменклатуры КАК ВтДанныеНоменклатуры
	|
	|СГРУППИРОВАТЬ ПО
	|	ВтДанныеНоменклатуры.Номенклатура,
	|	ВтДанныеНоменклатуры.ДатаРозлива
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеПМУ.СправкаБ,
	|	ДанныеПМУ.АлкогольнаяПродукция
	|ИЗ
	|	ДанныеПМУ КАК ДанныеПМУ
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеПМУ.СправкаБ,
	|	ДанныеПМУ.АлкогольнаяПродукция";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	ВыборкаНм=МассивРезультатов[0].Выбрать();
	ВыборкаПму=МассивРезультатов[1].Выбрать();
	Пока ВыборкаНм.Следующий() Цикл
		Элементы.ТаблицаСоответсвийНоменклатура.СписокВыбора.Добавить(ВыборкаНм.Номенклатура);
		Элементы.ТаблицаСоответсвийДатаРозлива.СписокВыбора.Добавить(ВыборкаНм.ДатаРозлива);
	КонецЦикла;
	Пока ВыборкаПму.Следующий() Цикл
		Элементы.ТаблицаСоответсвийСправкаБ.СписокВыбора.Добавить(ВыборкаПму.СправкаБ);
		Элементы.ТаблицаСоответсвийАлкогольнаяПродукция.СписокВыбора.Добавить(ВыборкаПму.АлкогольнаяПродукция);
	КонецЦикла;	
КонецПроцедуры

&НаСервере
Функция ТаблицаЗначенийВМассив(ИмяТаблицыНаФорме);
	Таблица=Вычислить(ИмяТаблицыНаФорме).Выгрузить();
	МассивТаблицы=новый Массив;
	Для Каждого стр из Таблица цикл
		СтруктураДанных=новый Структура;
		Для Каждого Колонка из Таблица.Колонки цикл
			СтруктураДанных.Вставить(Колонка.Имя,стр[Колонка.Имя]);	
		КонецЦикла;
		МассивТаблицы.Добавить(СтруктураДанных);
	КонецЦикла;
	Возврат МассивТаблицы;
КонецФункции