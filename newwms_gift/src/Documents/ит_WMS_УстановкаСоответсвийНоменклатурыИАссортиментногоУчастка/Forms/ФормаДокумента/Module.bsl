&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Ключ.Пустая() тогда
		Объект.Ответственный=ПараметрыСеанса.ТекущийПользователь;
		Объект.СтатусДокумента=Перечисления.итWMSСтатусыСкладскихДокументов.Создан;
		Объект.Дата=ТекущаяДата();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Подбор(Команда)
	СтруктураПараметров=новый Структура;
	СтруктураПараметров.Вставить("МассивНоменклатурыИсключения",МассивНоменклатурыИсключения());
	СтруктураПараметров.Вставить("Организация",Объект.Организация);
	ОткрытьФорму("Документ.ит_WMS_УстановкаСоответсвийНоменклатурыИАссортиментногоУчастка.Форма.ФормаПодбора",СтруктураПараметров,ЭтаФорма);
КонецПроцедуры

&НаСервере
Функция МассивНоменклатурыИсключения()
Возврат	Объект.СписокНоменклатуры.Выгрузить().ВыгрузитьКолонку("Номенклатура");
КонецФункции

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
		Если  ИсточникВыбора.ИмяФормы="Документ.ит_WMS_УстановкаСоответсвийНоменклатурыИАссортиментногоУчастка.Форма.ФормаПодбора" Тогда 
			ДобавлениеНоменклатурыНаСервере(ВыбранноеЗначение);
			ЭтаФорма.Модифицированность=Истина;
		КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура  ДобавлениеНоменклатурыНаСервере(МассивНоменклатуры)
ТаблицаДляАнализа=новый ТаблицаЗначений;
ТаблицаДляАнализа.Колонки.Добавить("Номенклатура",новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
Для Каждого стр из МассивНоменклатуры Цикл 
	ТаблицаДляАнализа.Добавить().Номенклатура=стр;
КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаДляАнализа.Номенклатура
		|ПОМЕСТИТЬ Вт
		|ИЗ
		|	&ТаблицаДляАнализа КАК ТаблицаДляАнализа
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Вт.Номенклатура
		|ИЗ
		|	Вт КАК Вт
		|
		|СГРУППИРОВАТЬ ПО
		|	Вт.Номенклатура";
	
	Запрос.УстановитьПараметр("ТаблицаДляАнализа",ТаблицаДляАнализа);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Объект.СписокНоменклатуры.Очистить();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		НоваяСтрока=Объект.СписокНоменклатуры.Добавить();
		НоваяСтрока.Номенклатура=ВыборкаДетальныеЗаписи.Номенклатура;
	КонецЦикла;
	
	КонецПроцедуры

&НаКлиенте
Процедура СоздатьПеремещения(Команда)
	Оповещение=новый ОписаниеОповещения("СоздатьПеремещенияОповещения",ЭтаФорма);
	ПоказатьВопрос(Оповещение,"Созданные перемещения будут сразу записанны и проведены, вы точно хотите продолжить?",РежимДиалогаВопрос.ДаНет);
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПеремещенияОповещения(Результат,Параметры) Экспорт
	Если Результат=КодВозвратаДиалога.Нет Тогда 
		Возврат
	КонецЕсли;	
	Если Объект.Организация.Пустая() Тогда 
		Сообщить("Заполните организацию");
		Возврат
	КонецЕсли;
	Если Объект.НовыйАссортиметныйУчасток.Пустая() Тогда 
		Сообщить("Заполните Ассортиментный участок");
		Возврат
	КонецЕсли;	
	Если Объект.Проведен и не ЭтаФорма.Модифицированность Тогда 
		СоздатьПеремещенияСервер();
	иначе
		Сообщить("в начале необходимо провести документ");
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура СоздатьПеремещенияСервер()
Настройки=итWMSПривилегированныйМодуль.ПолучитьНастройкиИзХранилища();
ЗонаКарантина=Настройки.ЗонаКарантина;
СистемнаяЯчейка=Настройки.СистемнаяЯчейка;
	//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ит_WMS_СоответсвияНоменклатурыИАссортиментногоУчасткаСрезПоследних.Номенклатура
		|ПОМЕСТИТЬ ВтНоменклатура
		|ИЗ
		|	РегистрСведений.ит_WMS_СоответсвияНоменклатурыИАссортиментногоУчастка.СрезПоследних(, Период = &Дата) КАК ит_WMS_СоответсвияНоменклатурыИАссортиментногоУчасткаСрезПоследних
		|ГДЕ
		|	ит_WMS_СоответсвияНоменклатурыИАссортиментногоУчасткаСрезПоследних.Организация = &Организация
		|	И ит_WMS_СоответсвияНоменклатурыИАссортиментногоУчасткаСрезПоследних.АссортиментныйУчастокСклада = &АссортиментныйУчастокСклада
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	итТоварыВЯчейкахОстатки.Склад,
		|	итТоварыВЯчейкахОстатки.Ячейка,
		|	итТоварыВЯчейкахОстатки.ИдентификаторУпаковки,
		|	итТоварыВЯчейкахОстатки.Номенклатура,
		|	итТоварыВЯчейкахОстатки.Характеристика,
		|	итТоварыВЯчейкахОстатки.СерияНоменклатуры,
		|	итТоварыВЯчейкахОстатки.ДатаРозлива,
		|	итТоварыВЯчейкахОстатки.Качество,
		|	итТоварыВЯчейкахОстатки.КоличествоОстаток
		|ПОМЕСТИТЬ ВтДанныеОстатков
		|ИЗ
		|	ВтНоменклатура КАК ВтНоменклатура
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.итТоварыВЯчейках.Остатки(
		|				,
		|				Ячейка.СкладскоеПомещение <> &ЗонаКарантина
		|					И Склад <> ЗНАЧЕНИЕ(Справочник.Склады.ПустаяССылка)) КАК итТоварыВЯчейкахОстатки
		|		ПО ВтНоменклатура.Номенклатура = итТоварыВЯчейкахОстатки.Номенклатура
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	итТоварыВЯчейкахРезервОстатки.Склад,
		|	итТоварыВЯчейкахРезервОстатки.ЯчейкаОтправитель,
		|	итТоварыВЯчейкахРезервОстатки.ИдентификаторУпаковки,
		|	итТоварыВЯчейкахРезервОстатки.Номенклатура,
		|	итТоварыВЯчейкахРезервОстатки.Характеристика,
		|	итТоварыВЯчейкахРезервОстатки.СерияНоменклатуры,
		|	итТоварыВЯчейкахРезервОстатки.ДатаРозлива,
		|	итТоварыВЯчейкахРезервОстатки.Качество,
		|	-итТоварыВЯчейкахРезервОстатки.КоличествоОстаток
		|ИЗ
		|	ВтНоменклатура КАК ВтНоменклатура
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.итТоварыВЯчейкахРезерв.Остатки(
		|				,
		|				Ячейка = &СистемнаяЯчейка
		|					И ЯчейкаОтправитель.СкладскоеПомещение <> &ЗонаКарантина
		|					И Склад <> ЗНАЧЕНИЕ(Справочник.Склады.ПустаяССылка)) КАК итТоварыВЯчейкахРезервОстатки
		|		ПО ВтНоменклатура.Номенклатура = итТоварыВЯчейкахРезервОстатки.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВтДанныеОстатков.Склад,
		|	ВтДанныеОстатков.Ячейка,
		|	ВтДанныеОстатков.ИдентификаторУпаковки,
		|	ВтДанныеОстатков.Номенклатура,
		|	ВтДанныеОстатков.Характеристика,
		|	ВтДанныеОстатков.СерияНоменклатуры,
		|	ВтДанныеОстатков.ДатаРозлива,
		|	ВтДанныеОстатков.Качество,
		|	СУММА(ВтДанныеОстатков.КоличествоОстаток) КАК КоличествоОстаток
		|ПОМЕСТИТЬ ВтГруппировкаОстатков
		|ИЗ
		|	ВтДанныеОстатков КАК ВтДанныеОстатков
		|
		|СГРУППИРОВАТЬ ПО
		|	ВтДанныеОстатков.Склад,
		|	ВтДанныеОстатков.Ячейка,
		|	ВтДанныеОстатков.ИдентификаторУпаковки,
		|	ВтДанныеОстатков.Номенклатура,
		|	ВтДанныеОстатков.Характеристика,
		|	ВтДанныеОстатков.СерияНоменклатуры,
		|	ВтДанныеОстатков.ДатаРозлива,
		|	ВтДанныеОстатков.Качество
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ит_WMS_АссортиментныеУчасткиСкладаСоставЯчеек.Ячейка
		|ПОМЕСТИТЬ ЯчейкаУчасткаАссортимента
		|ИЗ
		|	Справочник.ит_WMS_АссортиментныеУчасткиСклада.СоставЯчеек КАК ит_WMS_АссортиментныеУчасткиСкладаСоставЯчеек
		|ГДЕ
		|	ит_WMS_АссортиментныеУчасткиСкладаСоставЯчеек.Ссылка = &АссортиментныйУчастокСклада
		|
		|СГРУППИРОВАТЬ ПО
		|	ит_WMS_АссортиментныеУчасткиСкладаСоставЯчеек.Ячейка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВтГруппировкаОстатков.Склад КАК Склад,
		|	ВтГруппировкаОстатков.Ячейка КАК Ячейка,
		|	ВтГруппировкаОстатков.ИдентификаторУпаковки КАК ИдентификаторУпаковки,
		|	ВтГруппировкаОстатков.Номенклатура,
		|	ВтГруппировкаОстатков.Характеристика,
		|	ВтГруппировкаОстатков.СерияНоменклатуры,
		|	ВтГруппировкаОстатков.ДатаРозлива,
		|	ВтГруппировкаОстатков.Качество,
		|	ВтГруппировкаОстатков.КоличествоОстаток
		|ИЗ
		|	ВтГруппировкаОстатков КАК ВтГруппировкаОстатков
		|ГДЕ
		|	НЕ ВтГруппировкаОстатков.Ячейка В
		|				(ВЫБРАТЬ
		|					ЯчейкаУчасткаАссортимента.Ячейка
		|				ИЗ
		|					ЯчейкаУчасткаАссортимента КАК ЯчейкаУчасткаАссортимента)
		|	И ВтГруппировкаОстатков.КоличествоОстаток > 0
		|ИТОГИ ПО
		|	Склад,
		|	Ячейка,
		|	ИдентификаторУпаковки";
	
	Запрос.УстановитьПараметр("АссортиментныйУчастокСклада", Объект.НовыйАссортиметныйУчасток);
	Запрос.УстановитьПараметр("ЗонаКарантина", ЗонаКарантина);
	Запрос.УстановитьПараметр("СистемнаяЯчейка", СистемнаяЯчейка);
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("Дата",Объект.Дата);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаСклад = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаСклад.Следующий() Цикл
		МенеджерВременныхТаблиц=новый МенеджерВременныхТаблиц;
		СформироватьВременнуюТаблицаСвободныхЯчеек(МенеджерВременныхТаблиц,Настройки);
		ДокументПеремещения = СоздатьОбъектПеремещения(ВыборкаСклад);

		ВыборкаЯчейка=ВыборкаСклад.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаЯчейка.Следующий() цикл
			
			ВыборкаИдентификаторУпаковки=ВыборкаЯчейка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			СчетчикИдентификаторов=0;
			Пока ВыборкаИдентификаторУпаковки.Следующий() Цикл 
				Если СчетчикИдентификаторов>=50 Тогда 
					ЗаписатьОбъектПеремещения(ДокументПеремещения);
					МенеджерВременныхТаблиц=новый МенеджерВременныхТаблиц;
					СформироватьВременнуюТаблицаСвободныхЯчеек(МенеджерВременныхТаблиц,Настройки);
					ДокументПеремещения = СоздатьОбъектПеремещения(ВыборкаСклад);
                    СчетчикИдентификаторов=0;
				КонецЕсли;
				ВыборкаДетальныеЗаписи = ВыборкаИдентификаторУпаковки.Выбрать();
				ЯчейкаПолучатель=ПолучитьЯчейкуПолучатель(МенеджерВременныхТаблиц);
				
				Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				Если ЯчейкаПолучатель=Неопределено Тогда
						Сообщить(" закончились свободные ячейки в участке , номенклатура "+Строка(ВыборкаДетальныеЗаписи.Номенклатура)+" в количестве "+Строка(ВыборкаДетальныеЗаписи.Количество)+
						" перемещенная не будет ");
						Продолжить;
					КонецЕсли;
					НоваяСтрока=ДокументПеремещения.Товары.Добавить();
					НоваяСтрока.ИдентификаторСтроки=новый УникальныйИдентификатор;
					НоваяСтрока.ИдентификаторУпаковки=ВыборкаДетальныеЗаписи.ИдентификаторУпаковки;
					НоваяСтрока.ДатаРозлива=ВыборкаДетальныеЗаписи.ДатаРозлива;
					НоваяСтрока.Номенклатура=ВыборкаДетальныеЗаписи.Номенклатура;
					НоваяСтрока.Качество=ВыборкаДетальныеЗаписи.Качество;
					НоваяСтрока.Характеристика=ВыборкаДетальныеЗаписи.Характеристика;
					НоваяСтрока.Количество=ВыборкаДетальныеЗаписи.КоличествоОстаток;
					НоваяСтрока.СерияНоменклатуры=ВыборкаДетальныеЗаписи.СерияНоменклатуры;
					НоваяСтрока.ЯчейкаОтправитель=ВыборкаДетальныеЗаписи.Ячейка;
					НоваяСтрока.ЯчейкаПолучатель=ЯчейкаПолучатель;
					
				КонецЦикла;
				СчетчикИдентификаторов=СчетчикИдентификаторов+1;

				Если ЯчейкаПолучатель<>Неопределено Тогда 
					ИзъятьПаллетМестоИзЯчейки(МенеджерВременныхТаблиц,ЯчейкаПолучатель);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		ЗаписатьОбъектПеремещения(ДокументПеремещения);

	КонецЦикла;
	
	//}}КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА


	КонецПроцедуры
	
&НаСервере
Функция СоздатьОбъектПеремещения(ВыборкаСклад)
		
		Перем ДокументПеремещения;
		
		ДокументПеремещения=Документы.итWMSПеремещение.СоздатьДокумент();
		ДокументПеремещения.СкладОтправитель= ВыборкаСклад.Склад;
		ДокументПеремещения.СкладПолучатель=ВыборкаСклад.Склад;
		ДокументПеремещения.Организация =Объект.Организация;
		ДокументПеремещения.итОснование=Объект.Ссылка;
		ДокументПеремещения.Дата=ТекущаяДата();
		ДокументПеремещения.СтатусДокумента=Перечисления.итWMSСтатусыСкладскихДокументов.Создан;
		Возврат ДокументПеремещения;

КонецФункции
	
&НаСервере
Процедура ЗаписатьОбъектПеремещения(ДокументПеремещения)
		
		ДокументПеремещения.Комментарий="#Документ создан на основании изменения участка ассортимента товара ";
		Если ДокументПеремещения.Товары.Количество()>0 Тогда 
			ДокументПеремещения.Записать(РежимЗаписиДокумента.Проведение);
			Сообщить("Создан документ перемещения "+Строка(ДокументПеремещения.Ссылка));
		КонецЕсли;
КонецПроцедуры
		
&НаСервере	
Процедура ИзъятьПаллетМестоИзЯчейки(МенеджерВременныхТаблиц,Ячейка)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц=МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДанныеЯчеек.Ячейка,
		|	ДанныеЯчеек.СвободноеКолличествоПаллет
		|ПОМЕСТИТЬ ВтДанные
		|ИЗ
		|	ДанныеЯчеек КАК ДанныеЯчеек
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&Ячейка,
		|	-1
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВтДанные.Ячейка,
		|	СУММА(ВтДанные.СвободноеКолличествоПаллет) КАК СвободноеКолличествоПаллет
		|ПОМЕСТИТЬ ВтДанныеГруппировка
		|ИЗ
		|	ВтДанные КАК ВтДанные
		|
		|СГРУППИРОВАТЬ ПО
		|	ВтДанные.Ячейка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ДанныеЯчеек
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВтДанныеГруппировка.Ячейка,
		|	ВтДанныеГруппировка.СвободноеКолличествоПаллет
		|ПОМЕСТИТЬ ДанныеЯчеек
		|ИЗ
		|	ВтДанныеГруппировка КАК ВтДанныеГруппировка
		|ГДЕ
		|	ВтДанныеГруппировка.СвободноеКолличествоПаллет >= 1
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВтДанные
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВтДанныеГруппировка";
	
	Запрос.УстановитьПараметр("Ячейка",Ячейка);
	Запрос.Выполнить();
			
КонецПроцедуры
&НаСервере	
Функция ПолучитьЯчейкуПолучатель(МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц=МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ДанныеЯчеек.Ячейка
		|ИЗ
		|	ДанныеЯчеек КАК ДанныеЯчеек";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Возврат  ВыборкаДетальныеЗаписи.Ячейка;
	КонецЦикла;
	     Возврат Неопределено;
	
	
	КонецФункции
&НаСервере	
Процедура  СформироватьВременнуюТаблицаСвободныхЯчеек(МенеджерВременныхТаблиц,Настройки)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц=МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ит_WMS_АссортиментныеУчасткиСкладаСоставЯчеек.Ячейка
		|ПОМЕСТИТЬ ВтЯчейкиУчастка
		|ИЗ
		|	Справочник.ит_WMS_АссортиментныеУчасткиСклада.СоставЯчеек КАК ит_WMS_АссортиментныеУчасткиСкладаСоставЯчеек
		|ГДЕ
		|	ит_WMS_АссортиментныеУчасткиСкладаСоставЯчеек.Ссылка = &АссортиментныйУчасток
		|	И ит_WMS_АссортиментныеУчасткиСкладаСоставЯчеек.Ячейка.ВидСкладскойДеятельности = &ВидСкладскойДеятельности
		|СГРУППИРОВАТЬ ПО
		|	ит_WMS_АссортиментныеУчасткиСкладаСоставЯчеек.Ячейка
		|;
		|
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	итТоварыВЯчейкахОстатки.Ячейка,
		|	итТоварыВЯчейкахОстатки.Номенклатура,
		|	итТоварыВЯчейкахОстатки.КоличествоОстаток + итТоварыВЯчейкахОстатки.КРазмещениюОстаток / ВЫБОР
		|		КОГДА ЕСТЬNULL(итТоварыВЯчейкахОстатки.Номенклатура.ЕдиницаИзмеренияМест.итКоличествоНаПаллете, 1) = 0
		|			ТОГДА 1
		|		ИНАЧЕ ЕСТЬNULL(итТоварыВЯчейкахОстатки.Номенклатура.ЕдиницаИзмеренияМест.итКоличествоНаПаллете, 1)
		|	КОНЕЦ КАК КоличествоПаллет,
		|	итТоварыВЯчейкахОстатки.Номенклатура.ЕдиницаИзмеренияМест.итКоличествоНаПаллете КАК
		|		НоменклатураЕдиницаХраненияОстатковитКоличествоНаПаллете,
		|	итТоварыВЯчейкахОстатки.Ячейка.КоличествоПалет
		|ПОМЕСТИТЬ ВтЯчейкиСОстатками
		|ИЗ
		|	РегистрНакопления.итТоварыВЯчейках.Остатки КАК итТоварыВЯчейкахОстатки
		|;
		|
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.Ячейка,
		|	ВложенныйЗапрос.ЯчейкаКоличествоПалет - ВложенныйЗапрос.КоличествоПаллет КАК СвободноеКолличествоПаллет
		|ПОМЕСТИТЬ ЯчейкиСОстаткомПодПаллеты
		|ИЗ
		|	(ВЫБРАТЬ
		|		ВтЯчейкиСОстатками.Ячейка КАК Ячейка,
		|		СУММА(ВтЯчейкиСОстатками.КоличествоПаллет) КАК КоличествоПаллет,
		|		МАКСИМУМ(ВтЯчейкиСОстатками.ЯчейкаКоличествоПалет) КАК ЯчейкаКоличествоПалет
		|	ИЗ
		|		ВтЯчейкиСОстатками КАК ВтЯчейкиСОстатками
		|	СГРУППИРОВАТЬ ПО
		|		ВтЯчейкиСОстатками.Ячейка) КАК ВложенныйЗапрос
		|ГДЕ
		|	ВложенныйЗапрос.ЯчейкаКоличествоПалет - ВложенныйЗапрос.КоличествоПаллет >= 1
		|;
		|
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВтЯчейкиУчастка.Ячейка,
		|	ВтЯчейкиУчастка.Ячейка.КоличествоПалет КАК СвободноеКолличествоПаллет
		|ПОМЕСТИТЬ ВтПодготовкаДанныхЯчеек
		|ИЗ
		|	ВтЯчейкиУчастка КАК ВтЯчейкиУчастка
		|ГДЕ
		|	НЕ ВтЯчейкиУчастка.Ячейка В
		|		(ВЫБРАТЬ
		|			ВтЯчейкиСОстатками.Ячейка
		|		ИЗ
		|			ВтЯчейкиСОстатками КАК ВтЯчейкиСОстатками)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЯчейкиСОстаткомПодПаллеты.Ячейка,
		|	ЯчейкиСОстаткомПодПаллеты.СвободноеКолличествоПаллет
		|ИЗ
		|	ВтЯчейкиУчастка КАК ВтЯчейкиУчастка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЯчейкиСОстаткомПодПаллеты КАК ЯчейкиСОстаткомПодПаллеты
		|		ПО ВтЯчейкиУчастка.Ячейка = ЯчейкиСОстаткомПодПаллеты.Ячейка
		|;
		|
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВтПодготовкаДанныхЯчеек.Ячейка,
		|	СУММА(ВтПодготовкаДанныхЯчеек.СвободноеКолличествоПаллет) КАК СвободноеКолличествоПаллет
		|ПОМЕСТИТЬ ДанныеЯчеек
		|ИЗ
		|	ВтПодготовкаДанныхЯчеек КАК ВтПодготовкаДанныхЯчеек
		|ГДЕ
		|	НЕ ВтПодготовкаДанныхЯчеек.Ячейка.Заблокирована
		|	И НЕ ВтПодготовкаДанныхЯчеек.Ячейка.ПометкаУдаления
		|СГРУППИРОВАТЬ ПО
		|	ВтПодготовкаДанныхЯчеек.Ячейка
		|;
		|
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВтЯчейкиУчастка
		|;
		|
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВтЯчейкиСОстатками
		|;
		|
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ЯчейкиСОстаткомПодПаллеты
		|;
		|
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВтПодготовкаДанныхЯчеек";
	
	Запрос.УстановитьПараметр("АссортиментныйУчасток", Объект.НовыйАссортиметныйУчасток);
	Запрос.УстановитьПараметр("СистемнаяЯчейка", Настройки.СистемнаяЯчейка);
	Запрос.УстановитьПараметр("ВидСкладскойДеятельности",Настройки.ВидСкладскойДеятельности);

	Запрос.Выполнить();
	
	КонецПроцедуры
