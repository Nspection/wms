
Процедура ПередЗаписью(Отказ)
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;
	ЗаполнитьВидыВремениГрафика();
КонецПроцедуры

Процедура ЗаполнитьВидыВремениГрафика()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ГрафикиРаботыСотрудниковДанныеОРабочихЧасах.ВидВремени КАК ВидВремени
		|ИЗ
		|	Справочник.ГрафикиРаботыСотрудников.ДанныеОРабочихЧасах КАК ГрафикиРаботыСотрудниковДанныеОРабочихЧасах
		|ГДЕ
		|	ГрафикиРаботыСотрудниковДанныеОРабочихЧасах.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ГрафикиРаботыСотрудниковДанныеОРабочихЧасах.ВидВремени";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	ВидыВремени.Очистить();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		НоваяСтрока=ВидыВремени.Добавить();
		НоваяСтрока.ВидВремени=ВыборкаДетальныеЗаписи.ВидВремени;
	КонецЦикла;
	
	
КонецПроцедуры

Процедура ЗаполнитьТабличныйДокументГрафика(ТабличныйДокумент,Год) Экспорт 
	ТабличныйДокумент=новый ТабличныйДокумент;
	Макет=ПолучитьМакет("ШаблонГрафика");
	ШапкаОбщая=Макет.ПолучитьОбласть("Шапка|Основная");
	ТабличныйДокумент.Вывести(ШапкаОбщая);
	для n=0 по 30 Цикл 
		ШапкаДень=Макет.ПолучитьОбласть("Шапка|День");
		ШапкаДень.Параметры.ДеньШапка=n+1;
		ТабличныйДокумент.Присоединить(ШапкаДень);
	КонецЦикла;
	ДатаГода=Дата(Год,01,01);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ГрафикиРаботыПоВидамВремени.Месяц КАК Месяц,
		|	ГрафикиРаботыПоВидамВремени.ВидУчетаВремени КАК ВидУчетаВремени,
		|	ГрафикиРаботыПоВидамВремени.Дата КАК Дата,
		|	ГрафикиРаботыПоВидамВремени.ДополнительноеЗначение КАК ДополнительноеЗначение
		|ПОМЕСТИТЬ ВтГрафик
		|ИЗ
		|	РегистрСведений.ГрафикиРаботыПоВидамВремени КАК ГрафикиРаботыПоВидамВремени
		|ГДЕ
		|	ГрафикиРаботыПоВидамВремени.ГрафикРаботы = &ГрафикРаботы
		|	И ГрафикиРаботыПоВидамВремени.Месяц МЕЖДУ &ДатаНачала И &ДатаОкончания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ГрафикиРаботыСотрудниковВидыВремени.ВидВремени КАК ВидВремени
		|ПОМЕСТИТЬ ВтВидыВремени
		|ИЗ
		|	Справочник.ГрафикиРаботыСотрудников.ВидыВремени КАК ГрафикиРаботыСотрудниковВидыВремени
		|ГДЕ
		|	ГрафикиРаботыСотрудниковВидыВремени.Ссылка = &ГрафикРаботы
		|	И ГрафикиРаботыСотрудниковВидыВремени.ВидВремени <> ЗНАЧЕНИЕ(Справочник.ВидыИспользованияРабочегоВремени.ВыходныеДни)
		|	И ГрафикиРаботыСотрудниковВидыВремени.ВидВремени <> ЗНАЧЕНИЕ(Справочник.ВидыИспользованияРабочегоВремени.РабочееВремя)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВтГрафик.Месяц КАК Месяц,
		|	ВтГрафик.ВидУчетаВремени КАК ВидУчетаВремени,
		|	ВтГрафик.Дата КАК Дата
		|ПОМЕСТИТЬ ВтРабочиеВыходныеДни
		|ИЗ
		|	ВтГрафик КАК ВтГрафик
		|ГДЕ
		|	(ВтГрафик.ВидУчетаВремени = ЗНАЧЕНИЕ(Справочник.ВидыИспользованияРабочегоВремени.ВыходныеДни)
		|			ИЛИ ВтГрафик.ВидУчетаВремени = ЗНАЧЕНИЕ(Справочник.ВидыИспользованияРабочегоВремени.РабочееВремя))
		|
		|СГРУППИРОВАТЬ ПО
		|	ВтГрафик.Месяц,
		|	ВтГрафик.ВидУчетаВремени,
		|	ВтГрафик.Дата
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВтГрафик.Месяц КАК Месяц,
		|	ВтГрафик.Дата КАК Дата,
		|	ВтВидыВремени.ВидВремени КАК ВидВремени
		|ПОМЕСТИТЬ ГрафикДанные
		|ИЗ
		|	ВтГрафик КАК ВтГрафик
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтВидыВремени КАК ВтВидыВремени
		|		ПО (ИСТИНА)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВтГрафик.Месяц,
		|	ВтГрафик.Дата,
		|	ВтВидыВремени.ВидВремени
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВтГрафик.Месяц КАК Месяц,
		|	ВтГрафик.ВидУчетаВремени КАК ВидУчетаВремени,
		|	ВтГрафик.Дата КАК Дата,
		|	ВтГрафик.ДополнительноеЗначение КАК ДополнительноеЗначение
		|ПОМЕСТИТЬ ГрафикВидыВремени
		|ИЗ
		|	ВтГрафик КАК ВтГрафик
		|ГДЕ
		|	ВтГрафик.ВидУчетаВремени <> ЗНАЧЕНИЕ(Справочник.ВидыИспользованияРабочегоВремени.ВыходныеДни)
		|	И ВтГрафик.ВидУчетаВремени <> ЗНАЧЕНИЕ(Справочник.ВидыИспользованияРабочегоВремени.РабочееВремя)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ГрафикДанные.Месяц КАК Месяц,
		|	ГрафикДанные.Дата КАК Дата,
		|	ВтРабочиеВыходныеДни.ВидУчетаВремени КАК ВидДня,
		|	ГрафикДанные.ВидВремени КАК ВидУчетаВремени,
		|	ГрафикВидыВремени.ДополнительноеЗначение КАК ДополнительноеЗначение
		|ИЗ
		|	ГрафикДанные КАК ГрафикДанные
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтРабочиеВыходныеДни КАК ВтРабочиеВыходныеДни
		|		ПО ГрафикДанные.Месяц = ВтРабочиеВыходныеДни.Месяц
		|			И ГрафикДанные.Дата = ВтРабочиеВыходныеДни.Дата
		|		ЛЕВОЕ СОЕДИНЕНИЕ ГрафикВидыВремени КАК ГрафикВидыВремени
		|		ПО ГрафикДанные.Месяц = ГрафикВидыВремени.Месяц
		|			И ГрафикДанные.Дата = ГрафикВидыВремени.Дата
		|			И ГрафикДанные.ВидВремени = ГрафикВидыВремени.ВидУчетаВремени
		|
		|СГРУППИРОВАТЬ ПО
		|	ГрафикДанные.Месяц,
		|	ГрафикДанные.Дата,
		|	ГрафикВидыВремени.ДополнительноеЗначение,
		|	ВтРабочиеВыходныеДни.ВидУчетаВремени,
		|	ГрафикДанные.ВидВремени
		|ИТОГИ ПО
		|	Месяц,
		|	Дата
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВтВидыВремени.ВидВремени КАК ВидВремени
		|ИЗ
		|	ВтВидыВремени КАК ВтВидыВремени";
	
	Запрос.УстановитьПараметр("ГрафикРаботы", Ссылка);
    Запрос.УстановитьПараметр("ДатаНачала",ДатаГода);
	Запрос.УстановитьПараметр("ДатаОкончания",КонецГода(ДатаГода));

	МассивРезультатов = Запрос.ВыполнитьПакет();
	ВыборкаВидовВремени=МассивРезультатов[6].Выбрать();
	ВыборкаПоМесяцам=МассивРезультатов[5].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	КоличествоВидовВремени=МассивРезультатов[6].Выгрузить().Количество();
	Пока ВыборкаПоМесяцам.Следующий() Цикл
		ТабличныйДокМесяца=новый ТабличныйДокумент;
		ОбластьСтрокаМесяца=Макет.ПолучитьОбласть("СтрокаМесяц");
		ОбластьСтрокаМесяца.Параметры.Месяц=Формат(ВыборкаПоМесяцам.Месяц,"ДФ='ММММ'");
		ТабличныйДокМесяца.Вывести(ОбластьСтрокаМесяца);
		Если КоличествоВидовВремени >1 Тогда  
			Для n=2 по КоличествоВидовВремени Цикл 
				ОбластьСтрокаМесяца=Макет.ПолучитьОбласть("СтрокаМесяц");
				ТабличныйДокМесяца.Вывести(ОбластьСтрокаМесяца);
			КонецЦикла;
		КонецЕсли;
		ВыборкаВидовВремени.Сбросить();
		ТабличныйДокументВидовВремени=новый ТабличныйДокумент;
		Пока ВыборкаВидовВремени.Следующий() Цикл 
		ОбластьСтрокаВидВремени=Макет.ПолучитьОбласть("ВидВремениСтрока");
		ОбластьСтрокаВидВремени.Параметры.ВидВремени=ВыборкаВидовВремени.ВидВремени;
		ТабличныйДокументВидовВремени.Вывести(ОбластьСтрокаВидВремени);
		КонецЦикла;
		ТаблицаДеньЧас=новый ТабличныйДокумент;
		Для n=1 по КоличествоВидовВремени Цикл 
			ОбластьСтрокаДень=Макет.ПолучитьОбласть("ДеньСТрока");
			ОбластьСтрокаЧас=Макет.ПолучитьОбласть("ЧасСтрока");
            ТаблицаДеньЧас.Вывести(ОбластьСтрокаДень);
			ТаблицаДеньЧас.Присоединить(ОбластьСтрокаЧас);
		КонецЦикла;
		ТабличныйДокумент.Вывести(ТабличныйДокМесяца.ПолучитьОбласть(1,1,ТабличныйДокМесяца.ВысотаТаблицы,ТабличныйДокМесяца.ШиринаТаблицы));
		ТабличныйДокумент.Присоединить(ТабличныйДокументВидовВремени);
		ТабличныйДокумент.Присоединить(ТаблицаДеньЧас);
		ВыборкаДень=ВыборкаПоМесяцам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаДень.Следующий() Цикл
		ТабличныйДокументДень=новый ТабличныйДокумент;	
			
			ВыборкаДетальная=ВыборкаДень.Выбрать();
			СчетчикЗаписей=0;
			ВыходнойДень=Ложь;
			Пока ВыборкаДетальная.Следующий() Цикл
				СчетчикЗаписей=СчетчикЗаписей+1;
				ОбластьДняПоВидуВремени=Макет.ПолучитьОбласть("ДанныеДняСтрока");
				ОбластьДняПоВидуВремени.Параметры.ДанныеДня=ВыборкаДетальная.ДополнительноеЗначение;
				Если ВыборкаДетальная.ВидДня=Справочники.ВидыИспользованияРабочегоВремени.ВыходныеДни Тогда 
					ВыходнойДень=Истина;
					ОбластьДняПоВидуВремени.Область(1,1,ОбластьДняПоВидуВремени.ВысотаТаблицы,ОбластьДняПоВидуВремени.ШиринаТаблицы).ЦветФона=WebЦвета.Кремовый;
				КонецЕсли;	
				ТабличныйДокументДень.Вывести(ОбластьДняПоВидуВремени);
            КонецЦикла;
			Пока СчетчикЗаписей<КоличествоВидовВремени Цикл 
				ОбластьДняПоВидуВремени=Макет.ПолучитьОбласть("ДанныеДняСтрока");
				Если ВыходнойДень Тогда 
					ОбластьДняПоВидуВремени.Область(1,1,ОбластьДняПоВидуВремени.ВысотаТаблицы,ОбластьДняПоВидуВремени.ШиринаТаблицы).ЦветФона=WebЦвета.Кремовый;
				КонецЕсли;
				ТабличныйДокументДень.Вывести(ОбластьДняПоВидуВремени);	
				СчетчикЗаписей=СчетчикЗаписей+1;
			КонецЦикла;
		ТабличныйДокумент.Присоединить(ТабличныйДокументДень);	
        КонецЦикла;
	КонецЦикла;
	

	КонецПроцедуры